#Область Замечания //#ГК-УИВ 21.08.2025 { ITACMT-514

Функция ДанныеЗамечанийИкона(ТипОтклонения, ВидСобытия, ЗамечаниеСтройКонтроля = Ложь, ДатаОтбора = Неопределено)
	//#ГК-УИВ 02.09.2025 { ITACMT-514
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("DeviationType", ТипОтклонения);
	ДополнительныеПараметры.Вставить("DeviationNew", гк_ИнтеграцияИконаПовтИсп.НеОбработанныеОтклонения());
	
	Если ЗамечаниеСтройКонтроля Тогда
		ДополнительСписокДопПараметровОтборомПоДоговору(ДополнительныеПараметры);
	Иначе
		Если ЗначениеЗаполнено(ДатаОтбора) Тогда
			ДополнительныеПараметры.Вставить("fromDate",  Формат(ДатаОтбора, "ДФ=yyyy-MM-dd"));
		КонецЕсли;
	КонецЕсли;
	
	РезультатЗапроса = ВыполнитьHTTPЗапрос(ВидСобытия,, ДополнительныеПараметры);
	
	ОбработанныйОтвет = ПрочитатьОтветHttpЗапроса(РезультатЗапроса);
	
	ДанныеЗамечанийИкона = гк_HTTPКонекторУниверсальный.ЗначениеИзСтрокиJSON(ОбработанныйОтвет.ОбработанноеТелоОтвета,
		Истина);
		
	Возврат ДанныеЗамечанийИкона;
	//#ГК-УИВ 02.09.2025 } ITACMT-514
КонецФункции

Функция ОповеститьИконуОбОбработанномСообщении(ДанныеИкона, ТипОтклонения)
	//#ГК-УИВ 02.09.2025 { ITACMT-517
	ВидСобытияОтветИконе = ПредопределенноеЗначение("Перечисление.гк_ВидыСобытийИкона.SetProcessedDeviations");
	
	ПараметрыОтвета = гк_ИнтеграцияИконаПовтИсп.КонструкторОтветаОбОбработкеЗамечаний();
	ПараметрыОтвета.deviationTypeEnum = ТипОтклонения;
	ПараметрыОтвета.DeviationId = ДанныеИкона["DeviationId"];
	ПараметрыОтвета.DeviationNew = "false";
	
	ТелоЗапроса = гк_HTTPКонекторУниверсальный.ЗначениеВСтрокуJSON(ПараметрыОтвета);
	
	РезультатЗапроса = ВыполнитьHTTPЗапрос(ВидСобытияОтветИконе, ТелоЗапроса);
	//#ГК-УИВ 02.09.2025 } ITACMT-517
КонецФункции

Функция СоздатьДокументИЗафиксироватьОбработкуДанных(Знач ДанныеИкона, Знач ТипОтклонения, СоздаваемыйДокумент,
	Знач ВидСобытия, Знач ОбъектСтроительства, СписокОшибок)
	//#ГК-УИВ 02.09.2025 { ITACMT-517
	СоздаваемыйДокумент.УстановитьНовыйНомер();
	
	ДокументСоздан = Ложь;
	
	Попытка
		СоздаваемыйДокумент.Записать(РежимЗаписиДокумента.Запись);
		ДокументСоздан = Истина;
		ЗафиксироватьДанныеПереданныхФайлов(СоздаваемыйДокумент.Ссылка, ДанныеИкона);
	Исключение
		СписокОшибок.Добавить(СтрШаблон("Не удалось создать замечание по причине: %1", ОписаниеОшибки()));
	КонецПопытки;
	
	Если гк_ОбщегоНазначенияПовтИсп.ЭтоРабочаяБаза() И ДокументСоздан Тогда
		// Возвращаем ответ в ICONA только на рабоей базе.
		ОповеститьИконуОбОбработанномСообщении(ДанныеИкона, ТипОтклонения);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СписокОшибок) Тогда
		СписокОшибок.Вставить(0, "Список ошибок:");
	КонецЕсли;
	
	// Записываем историю обмена.
	РегистрыСведений.гк_ИсторияОбменаИкона.ЗаписатьИсториюОбмена(ТекущаяДатаСеанса(),
		СоздаваемыйДокумент.Ссылка,
		ВидСобытия,,
		ДанныеИконаСтрокой(ДанныеИкона),
		СтрСоединить(СписокОшибок, Символы.ПС),
		ОбъектСтроительства);
		
	Возврат СоздаваемыйДокумент.Ссылка;
	//#ГК-УИВ 02.09.2025 } ITACMT-517
КонецФункции

#Область ЗамечанияСтройКонтроля

Процедура СоздатьЗамечанияСтройКонтроля(Знач ТипОтклонения) Экспорт
	//#ГК-УИВ 28.08.2025 { ITACMT-514
	ВидСобытия = ПредопределенноеЗначение("Перечисление.гк_ВидыСобытийИкона.TNDeviations");
	
	ДанныеЗамечанийИкона = ДанныеЗамечанийИкона(ТипОтклонения, ВидСобытия, Истина, ТекущаяДатаСеанса());
	
	ЗамечаниеСтройКонтроляМенеджер = Документы.гк_ЗамечаниеСтройКонтроля;
	
	ХэшТаблицаКонтрагент                = Новый Соответствие;
	ХэшТаблицаДоговорыКонтрагентов      = Новый Соответствие;
	ХэшТаблицаИсполнительИОтветственный = Новый Соответствие;
	
	Предписание = Перечисления.гк_ВидОперацииЗамечаниеСК.Предписание;
	
	Для Каждого ДанныеЗамечанияИкона Из ДанныеЗамечанийИкона Цикл
		
		ОбъектСтроительства = ОбъектСтроительстваПоID(ДанныеЗамечанияИкона.Получить("ObjectId"));
		
		ДанныеЗамечанияСтройКонтроля = ДанныеЗамечанияИкона.Получить(гк_ИнтеграцияИконаПовтИсп.ОтклонениеTNDeviations());
		
		Для Каждого ДанныеИкона Из ДанныеЗамечанияСтройКонтроля Цикл
			
			СписокОшибок = Новый Массив;
			
			ЗамечаниеСтройКонтроля = ЗамечаниеСтройКонтроляМенеджер.СоздатьДокумент();
			ЗамечаниеСтройКонтроля.ОбменДанными.Загрузка = Истина;
			
			// Объект строительства
			ОбщегоНазначения.УстановитьЗначениеРеквизита(ЗамечаниеСтройКонтроля, "ОбъектСтроительства", ОбъектСтроительства);
			
			Если Не ЗначениеЗаполнено(ОбъектСтроительства) Тогда
				СписокОшибок.Добавить(СтрШаблон("Объект строительства (ид = %1) не найден в базе",
					Формат(ДанныеЗамечанияИкона["ObjectId"], "ЧН=0; ЧГ=0")));
			КонецЕсли;
			
			// Поиск договора
			ИдДоговора = ДанныеИкона["ContractId"];
			ДоговорКонтрагента = ДоговорКонтрагентаИзХэшТаблицы(ИдДоговора, ХэшТаблицаДоговорыКонтрагентов);
			
			ЭтоМПС = Ложь;
			КонтрагентМПС = Неопределено;
			
			// Договор контргаента. Поиск организации МПС.
			ОбработатьДанныеДоговораКонтрагента(ДанныеИкона, ЗамечаниеСтройКонтроля, ДоговорКонтрагента, ЭтоМПС, КонтрагентМПС,
				СписокОшибок);
			
			// Контрагент
			ИдКонтрагента = ДанныеИкона["ContractorId"];
			Контрагент = КонтрагентИзХэшТаблицы(ИдКонтрагента, ХэшТаблицаДоговорыКонтрагентов);
			
			Если ЗначениеЗаполнено(Контрагент) Тогда
				
				ЗаполняемыйРеквизит = ?(ЭтоМПС, "Субподрядчик", "Контрагент");
				
				ОбщегоНазначения.УстановитьЗначениеРеквизита(ЗамечаниеСтройКонтроля, ЗаполняемыйРеквизит, Контрагент);
				
				// Контактное лицо контрагента
				ОсновноеКонтактноеЛицо = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Контрагент, "ОсновноеКонтактноеЛицо");
				Если ЗначениеЗаполнено(ОсновноеКонтактноеЛицо) Тогда
					ЗаполняемыйРеквизит = ?(ЭтоМПС, "ОтветственныйОтСубподрядчика", "ОтветственныйОтКонтрагента");
						ОбщегоНазначения.УстановитьЗначениеРеквизита(ЗамечаниеСтройКонтроля, ЗаполняемыйРеквизит, ОсновноеКонтактноеЛицо);
				КонецЕсли;
			Иначе
				СписокОшибок.Добавить(СтрШаблон("Контрагент (ид = %1) не найден в базе",
					Формат(ДанныеИкона["ContractorId"], "ЧН=0; ЧГ=0")));
			КонецЕсли;
			
			// Дата
			ДатаДокумента = ПрочитатьДатуJSON(ДанныеИкона["FixationDate"], ФорматДатыJSON.ISO);
			ОбщегоНазначения.УстановитьЗначениеРеквизита(ЗамечаниеСтройКонтроля, "Дата", ДатаДокумента);
			
			// Описание
			ОбщегоНазначения.УстановитьЗначениеРеквизита(ЗамечаниеСтройКонтроля, "Описание", ДанныеИкона["Comment"]);
			
			// ВидОперации
			ОбщегоНазначения.УстановитьЗначениеРеквизита(ЗамечаниеСтройКонтроля, "гк_ВидОперации", Предписание);
			
			// МестоположениеДефекта
			МестоположениеДеффекта = СтрШаблон("%1, %2", ДанныеИкона["Level4"], ДанныеИкона["Level5"]);
			ОбщегоНазначения.УстановитьЗначениеРеквизита(ЗамещаниеСтройКонтроля, "МестоположениеДефекта", МестоположениеДеффекта);
			
			// ДатаУстранения
			ДатаУстранения = ПрочитатьДатуJSON(ДанныеИкона["CorrectionDeadline"], ФорматДатыJSON.ISO);
			ОбщегоНазначения.УстановитьЗначениеРеквизита(ЗамечаниеСтройКонтроля, "ДатаУстранения", ДатаУстранения);
			
			// Комментарий
			Комментарий = "Создан автоматически (Интеграция с Икона)";
			ОбщегоНазначения.УстановитьЗначениеРеквизита(ЗамечаниеСтройКонтроля, "Комментарий", Комментарий);
			
			// Исполнитель и ответственный
			ИДИнспектора  = ДанныеИкона["InspectorId"];
			ИмяИнспектора = ДанныеИкона["InspectorName"];
			
			ИсполнительИОтветственный = ИсполнительИОтветственныйИзХэшТаблицы(ИДИнспектора, ИмяИнспектора,
				ХэшТаблицаИсполнительИОтветственный, СписокОшибок);
			
			Если ЗначениеЗаполнено(ИсполнительИОтветственный) Тогда
				ОбщегоНазначения.УстановитьЗначениеРеквизита(ЗамечаниеСтройКонтроля, "Ответственный", ИсполнительИОтветственный);
				ОбщегоНазначения.УстановитьЗначениеРеквизита(ЗамечаниеСтройКонтроля, "Исполнитель", ИсполнительИОтветственный);
			КонецЕсли;
			
			// STK
			ОбщегоНазначения.УстановитьЗначениеРеквизита(ЗамечаниеСтройКонтроля, "STK_id"  , ДанныеИкона["STKId"]);
			ОбщегоНазначения.УстановитьЗначениеРеквизита(ЗамещаниеСтройКонтроля, "STK_name", ДанныеИкона["STKName"]);
			
			СоздатьДокументИЗафиксироватьОбработкуДанных(ДанныеИкона, ТипОтклонения, ЗамечаниеСтройКонтроля, ВидСобытия,
				ОбъектСтроительства, СписокОшибок);
				
		КонецЦикла;
			
	КонецЦикла;
	//#ГК-УИВ 28.08.2025 } ITACMT-514
КонецПроцедуры

#КонецОбласти

#Область ПредписанияТБиОТ

Процедура СоздатьПредписанияТБиОТ(Знач ТипОтклонения) Экспорт
	//#ГК-УИВ 01.09.2025 { ITACMT-517
	ВидСобытия = ПредопределенноеЗначение("Перечисление.гк_ВидыСобытийИкона.OhrTrudDeviations");
	ВидСобытияОтветИконе = ПредопределенноеЗначение("Перечисление.гк_ВидыСобытийИкона.SetProcessedDeviations");
	
	ДанныеЗамечанийИкона = ДанныеЗамечанийИкона(ТипОтклонения, ВидСобытия,, ТекущаяДатаСеанса());
	
	НарядНаПроверкуТБМенеджер = Документы.гк_НарядНаПроверкуТехникиБезопасности;
	НарушениеТБМенеджер = Документы.гк_НарушениеТехникиБезопасности;
	
	СтатусНарушения = ПредопределенноеЗначение("Перечисление.гк_ВидыСтатусовПредупреждений.НеВыполнено");
	
	ХэшТаблицаКонтрагент                = Новый Соответствие;
	ХэшТаблицаДоговорыКонтрагентов      = Новый Соответствие;
	ХэшТаблицаИсполнительИОтветственный = Новый Соответствие;
	
	Для Каждого ДанныеЗамещанияИкона Из ДанныеЗамещанияИкона Цикл
		
		ОбъектСтроительства = ОбъектСтроительстваПоID(ДанныеЗамещанияИкона.Получить("ObjectId"));
		
		ДанныеОхраныТруда = ДанныеЗамещанияИкона.Получить(гк_ИнтеграцияИконаПовтИсп.ОтклонениеOhrTrudDeviations());
		
		Для Каждого ДанныеИкона Из ДанныеОхраныТруда Цикл
			
			СписокОшибок = Новый Массив;
			
			ИдДоговора = ДанныеИкона["ContractId"];
			
			// Документ основание для Нарушения техники безопасности. Наряд на проверку ТБ.
			НарядНаПроверкуТБ = НарядНаПроверкуТБ(СписокОшибок, ДанныеИкона, ХэшТаблицаДоговорыКонтрагентов,
				ХэшТаблицаИсполнительИОтветственный, НарядНаПроверкуТБМенеджер, ОбъектСтроительства, ИдДоговора);
				
			НарушениеТБ = НарушениеТБМенеджер.СоздатьДокумент();
			НарушениеТБ.ОбменДанными.Загрузка = Истина;
			
			// Объект строительства, Организация, Комментарий, Дата
			ЗаполнитьЗначенияСвойств(НарушениеТБ, НарядНаПроверкуТБ);
			
			// ДокументОснование
			ОбщегоНазначения.УстановитьЗначениеРеквизита(НарушениеТБ, "ДокументОснование", НарядНаПроверкуТБ);
			
			// Договор
			ОбщегоНазначения.УстановитьЗначениеРеквизита(НарушениеТБ, "Договор", ХэшТаблицаДоговорыКонтрагентов[ИдДоговора]);
			
			// Контрагент
			Контрагент = КонтрагентИзХэшТаблицы(ДанныеИкона["ContractorId"], ХэшТаблицаКонтрагент);
			
			Если ЗначениеЗаполнено(Контрагент) Тогда
				ОбщегоНазначения.УстановитьЗначениеРеквизита(НарушениеТБ, "Подрядчик", Контрагент);
			Иначе
				СписокОшибок.Добавить(СтрШаблон("Контрагент (ид = %1) не найден в базе",
					Формат(ДанныеИкона["ContractorId"], "ЧН=0; ЧГ=0")));
			КонецЕсли;
			
			// ДатаПредписания
			ОбщегоНазначения.УстановитьЗначениеРеквизита(НарушениеТБ, "ДатаПредписания", НарушениеТБ.Дата);
			
			// ДнейНаУстранение
			ДатаУстранения = ДанныеИкона["PlannedCorrectionDate"];
			
			Если ЗначениеЗаполнено(ДатаУстранения) Тогда
				
				ДатаУстранения = ПрочитатьДатуJSON(ДатаУстранения, ФорматДатыJSON.ISO);
				
				ДнейНаУстранение = (НачалоДня(ДатаУстранения) - НачалоДня(НарушениеТБ.ДатаПредписания)) / (60 * 60 * 24);
				
				ОбщегоНазначения.УстановитьЗначениеРеквизита(НарушениеТБ, "ДнейНаУстранение", ДнейНаУстранение);
			Иначе
				СписокОшибок.Добавить("Дата устранения (PlannedCorrectionDate) не заполнена");
			КонецЕсли;
			
			СсылкаНаНарушение = СоздатьДокументИЗафиксироватьОбработкуДанных(ДанныеИкона, ТипОтклонения, НарушениеТБ, 
				ВидСобытия, ОбъектСтроительства, СписокОшибок);
				
			Если ЗначениеЗаполнено(СсылкаНаНарушение) Тогда
				// Фиксируем статус нарушения техники безопасности. Всегда "НеВыполнено"
				Таблица = РегистрыСведений.гк_СтатусыНарушенийТехникиБезопасности.СтруктураТаблицы();
					
				НоваяСтрока = Таблица.Добавить();
				НоваяСтрока.Нарушение   = СсылкаНаНарушение;
				НоваяСтрока.Статус      = СтатусНарушения;
				НоваяСтрока.ДатаСобытия = НарушениеТБ.Дата;
					
				РегистрыСведений.гк_СтатусыНарушенийТехникиБезопасности.ОтразитьСостояниеПредписания(Таблица, Ложь);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	//#ГК-УИВ 01.09.2025 } ITACMT-517
КонецПроцедуры

#КонецОбласти

#Область ЗаполнениеДокументов

Функция НарядНаПроверкуТБ(СписокОшибок, ДанныеИкона, ХэшТаблицаДоговорыКонтрагентов,
		ХэшТаблицаИсполнительИОтветственный, НарядНаПроверкуТБМенеджер, ОбъектСтроительства, ИдДоговора)
	//#ГК-УИВ 02.09.2025 { ITACMT-517
	НарядНаПроверкуТБ = НарядНаПроверкуТБМенеджер.СоздатьДокумент();
	НарядНаПроверкуТБ.ОбменДанными.Загрузка = Истина;
	
	// ДатаПроверки и Дата
	ДатаДокумента = ПрочитатьДатуJSON(ДанныеИкона["FixationDate"], ФорматДатыJSON.ISO);
	ОбщегоНазначения.УстановитьЗначениеРеквизита(НарядНаПроверкуТБ, "Дата", ДатаДокумента);
	ОбщегоНазначения.УстановитьЗначениеРеквизита(НарядНаПроверкуТБ, "ДатаПроверки", ДатаДокумента);
	
	// Комментарий
	Комментарий = "Создан автоматически (Интеграция с Икона)";
	ОбщегоНазначения.УстановитьЗначениеРеквизита(НарядНаПроверкуТБ, "Комментарий", Комментарий);
	
	// Объект строительства
	ОбщегоНазначения.УстановитьЗначениеРеквизита(НарядНаПроверкуТБ, "ОбъектСтроительства", ОбъектСтроительства);
	
	// Поиск договора
	ДоговорКонтрагента = ДоговорКонтрагентаИзХэшТаблицы(ИдДоговора, ХэшТаблицаДоговорыКонтрагентов);
	
	Если ЗначениеЗаполнено(ДоговорКонтрагента) Тогда
		ЗаполнитьДокументПоДоговору(ДоговорКонтрагента, ДанныеИкона, НарядНаПроверкуТБ);
	Иначе
		СписокОшибок.Добавить(СтрШаблон("Договор контрагента (ид = %1) не найден в базе",
		Формат(ДанныеИкона["ContractId"], "ЧН=0; ЧГ=0")));
	КонецЕсли;
	
	// Исполнитель и ответственный
	ИДИнспектора  = ДанныеИкона["InspectorId"];
	ИмяИнспектора = ДанныеИкона["InspectorName"];
	
	ИсполнительИОтветственный = ИсполнительИОтветственныйИзХэшТаблицы(ИДИнспектора, ИмяИнспектора,
		ХэшТаблицаИсполнительИОтветственный, СписокОшибок);
	
	Если ЗначениеЗаполнено(ИсполнительИОтветственный) Тогда
		ОбщегоНазначения.УстановитьЗначениеРеквизита(НарядНаПроверкуТБ,
			"Ответственный", ИсполнительИОтветственный);
		ОбщегоНазначения.УстановитьЗначениеРеквизита(НарядНаПроверкуТБ,
			"ОтветственныйЗаПроверку", ИсполнительИОтветственный);
		ОбщегоНазначения.УстановитьЗначениеРеквизита(НарядНаПроверкуТБ,
			"ОтветственноеЛицоВоВремяПроверки",ИсполнительИОтветственный);
	КонецЕсли;
	
	НарядНаПроверкуТБ.УстановитьНовыйНомер();
	Попытка
		НарядНаПроверкуТБ.Записать(РежимЗаписиДокумента.Запись);
	Исключение
		СписокОшибок.Добавить(СтрШаблон("Не удалось наряд на проверку ТБ по причине: %1", ОписаниеОшибки()));
	КонецПопытки;
	
	Возврат НарядНаПроверкуТБ.Ссылка;
	//#ГК-УИВ 02.09.2025 } ITACMT-517
КонецФункции

Функция ДоговорКонтрагентаИзХэшТаблицы(Знач ИдДоговора, ХэшТаблица)
	//#ГК-УИВ 02.09.2025 { ITACMT-517
	ДоговорКонтрагента = ХэшТаблица.Получить(ИдДоговора);
	
	Если ДоговорКонтрагента = Неопределено Тогда
		ДоговорКонтрагента = ДоговорКонтрагентаПоID(ИдДоговора);
		Если ЗначениеЗаполнено(ДоговорКонтрагента) Тогда
			ХэшТаблица.Вставить(ИдДоговора, ДоговорКонтрагента);
		КонецЕсли;
	КонецЕсли;
	
	Возврат ДоговорКонтрагента;
	//#ГК-УИВ 02.09.2025 } ITACMT-517
КонецФункции

Функция КонтрагентИзХэшТаблицы(Знач ИдКонтрагента, ХэшТаблица)
	//#ГК-УИВ 02.09.2025 { ITACMT-517
	Контрагент = ХэшТаблица.Получить(ИдКонтрагента);
	
	Если Контрагент = Неопределено Тогда
		Контрагент = КонтрагентПоID(ИдКонтрагента);
		Если ЗначениеЗаполнено(Контрагент) Тогда
			ХэшТаблица.Вставить(ИдКонтрагента, Контрагент);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Контрагент;
	//#ГК-УИВ 02.09.2025 } ITACMT-517
КонецФункции

Функция ИсполнительИОтветственныйИзХэшТаблицы(Знач ИДИнспектора, Знач ИмяИнспектора, ХэшТаблица, СписокОшибок)
	//#ГК-УИВ 02.09.2025 { ITACMT-517
	ИсполнительИОтветственный = ХэшТаблица.Получить(ИДИнспектора);
	
	Если ИсполнительИОтветственный = Неопределено Тогда
		ИсполнительИОтветственный = ИсполнительИОтветственный(ИДИнспектора, ИмяИнспектора,
			СписокОшибок);
		Если ЗначениеЗаполнено(ИсполнительИОтветственный) Тогда
			ХэшТаблица.Вставить(ИДИнспектора, ИсполнительИОтветственный);
		КонецЕсли;
	КонецЕсли;
	
	Возврат ИсполнительИОтветственный;
	//#ГК-УИВ 02.09.2025 } ITACMT-517
КонецФункции

Функция ИсполнительИОтветственный(Знач InspectorId, Знач InspectorName, СписокОшибок)
	//#ГК-УИВ 29.08.2025 { ITACMT-514
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	гк_ПользователиИкона.Пользователь1С КАК Исполнитель,
	|	гк_ПользователиИкона.Пользователь1С КАК Ответственный
	|ИЗ
	|	РегистрСведений.гк_ПользователиИкона КАК гк_ПользователиИкона
	|ГДЕ
	|	гк_ПользователиИкона.ПользовательID_ICONA = &InspectorId";
	
	Запрос.УстановитьПараметр("InspectorId", InspectorId);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Исполнитель;
	КонецЕсли;
	
	ИсполнительИОтветственный = Справочники.Пользователи.НайтиПоНаименованию(СокрЛП(InspectorName));
	
	Если Не ЗначениеЗаполнено(ИсполнительИОтветственный) Тогда
		ТекстОшибки = СтрШаблон("По ID Пользователи Икона %1 и имени %2 не нашлось соответствий в базе данных",
			InspectorId, InspectorName);
		СписокОшибок.Добавить(ТекстОшибки);
	КонецЕсли;
	
	РегистрыСведений.гк_ПользователиИкона.ЗаписатьПользователяИконы(InspectorId, InspectorName, ИсполнительИОтветственный);
	
	Возврат ИсполнительИОтветственный;
	//#ГК-УИВ 29.08.2025 } ITACMT-514
КонецФункции

Процедура ОбработатьДанныеДоговораКонтрагента(ДанныеИкона, ЗамечаниеСтройКонтроля, Знач ДоговорКонтрагента, Знач ЭтоМПС, КонтрагентМПС, СписокОшибок)
	//#ГК-УИВ 08.09.2025 { ITACMT-514
	Если ЗначениеЗаполнено(ДоговорКонтрагента) Тогда
		
		ДанныеДоговора = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДоговорКонтрагента,
			"Организация,гк_ДоговорПеревыставления");
		
		ЭтоМПС = гк_ОбщегоНазначенияПовтИсп.ЭтоМПС(ДанныеДоговора.Организация);
		// Если организация МПС - то заполняем документ договором перевыставления.
		Если ЭтоМПС Тогда
			
			ОбщегоНазначения.УстановитьЗначениеРеквизита(ЗамечаниеСтройКонтроля, "ДоговорСубподрядчика", ДоговорКонтрагента);
			
			ДоговорКонтрагента = ДанныеДоговора.гк_ДоговорПеревыставления;
			
			КонтрагентМПС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДоговорКонтрагента, "Владелец");
			
			Если ЗначениеЗаполнено(КонтрагентМПС) И ТипЗнч(КонтрагентМПС) = Тип("СправочникСсылка.Контрагенты") Тогда
				// Контрагент
				ОбщегоНазначения.УстановитьЗначениеРеквизита(ЗамечаниеСтройКонтроля, "Контрагент", КонтрагентМПС);
				
				// Основное контактное лицо.
				ОсновноеКонтактноеЛицо = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(КонтрагентМПС, "ОсновноеКонтактноеЛицо");
				Если ЗначениеЗаполнено(ОсновноеКонтактноеЛицо) Тогда
					ОбщегоНазначения.УстановитьЗначениеРеквизита(ЗамечаниеСтройКонтроля, "ОтветственныйОтКонтрагента",
					ОсновноеКонтактноеЛицо);
				КонецЕсли;
			Иначе
				СписокОшибок.Добавить("Не удалось установить владельца договора");
			КонецЕсли;
			
			// Перевыставление.
			ОбщегоНазначения.УстановитьЗначениеРеквизита(ЗамечаниеСтройКонтроля, "Перевыставлять", ЭтоМПС);
			
		КонецЕсли;
		
		ЗаполнитьДокументПоДоговору(ДоговорКонтрагента, ДанныеИкона, ЗамечаниеСтройКонтроля);
	Иначе
		СписокОшибок.Добавить(СтрШаблон("Договор контрагента (ид = %1) не найден в базе",
		Формат(ДанныеИкона["ContractId"], "ЧН=0; ЧГ=0")));
	КонецЕсли;
	
	//#ГК-УИВ 08.09.2025 } ITACMT-514
КонецПроцедуры

Процедура ЗаполнитьДокументПоДоговору(Знач ДоговорКонтрагента, Знач ДанныеЗамечанияИкона, ЗаполняемыйДокумент)
	//#ГК-УИВ 22.08.2025 { ITACMT-514
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДоговорыКонтрагентов.Ссылка КАК ДоговорКонтрагента,
	|	ДоговорыКонтрагентов.Организация КАК Организация,
	|	ДоговорыКонтрагентов.бит_Проект КАК Проект
	|ИЗ
	|	Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|ГДЕ
	|	ДоговорыКонтрагентов.Ссылка = &ДоговорКонтрагента";
	
	Запрос.УстановитьПараметр("ДоговорКонтрагента", ДоговорКонтрагента);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Выборка.Следующий();
	
	ЗаполнитьЗначенияСвойств(ЗаполняемыйДокумент, Выборка);
	//#ГК-УИВ 22.08.2025 } ITACMT-514
КонецПроцедуры

#КонецОбласти

#Область ФайлыИкона

Функция ДанныеФайлаИкона(Знач URL) Экспорт
	//#ГК-УИВ 29.08.2025 { ITACMT-514
	ДанныеФайлаИкона = Новый Структура;
	
	// URL Пример:
	// "https://moscowbrand.ru/wp-content/uploads/2016/12/www.jpg"
	
	// 1. Ищем значение в формате "://moscowbrand.ru"
	Паттерн = "://([^/]+)";
	РезультатПоиска = СтрНайтиПоРегулярномуВыражению(URL, Паттерн);
	
	Если Не ЗначениеЗаполнено(РезультатПоиска.Значение) Тогда
		ДанныеФайлаИкона.Вставить("Ошибка", "Некорректный путь к файлу.");
		Возврат ДанныеФайлаИкона;
	КонецЕсли;
	
	// 2. Фиксируем домен для HTTPСоединение "moscowbrand.ru"
	ДоменныйАдресФото = Сред(РезультатПоиска.Значение, 4);
	
	// 3. Фиксируем полный доменный адрес "https://moscowbrand.ru"
	ДоменныйАдресКартинкиCHTTPS = СтрНайтиПоРегулярномуВыражению(URL, "https://([^/]+)").Значение;
	
	// 4. Определяем путь к картинке (https://moscowbrand.ru отнимаем от URL)
	ПутьККартинке = Прав(URL, СтрДлина(URL) - СтрДлина(ДоменныйАдресКартинкиCHTTPS));
	
	// Результат :
	// ДоменныйАдресФото - moscowbrand.ru
	// ПутьКФайлу        - /wp-content/uploads/2016/12/www.jpg
	
	Соединение = Новый HTTPСоединение(ДоменныйАдресФото, 443,,,,, Новый ЗащищенноеСоединениеOpenSSL());
	
	Запрос = Новый HTTPЗапрос(ПутьККартинке);
	Результат = Соединение.Получить(Запрос);
	
	Если Результат.КодСостояния <> 200 Тогда
		ДанныеФайлаИкона.Вставить("Ошибка", "Не удалось получить файл.");
		Возврат ДанныеФайлаИкона;
	КонецЕсли;
	
	// 5. Определяем рашсирение после точки .../www.jpg@1000x1000 => jpg
	Паттерн = "(?<=\.)([a-zA-Z0-9]+)(?=@|\?|$)";
	Расширение = СтрНайтиПоРегулярномуВыражению(URL, Паттерн).Значение;
	
	ДвоичныеДанныеФотографии = Результат.ПолучитьТелоКакДвоичныеДанные();
	
	ДанныеФайлаИкона.Вставить("ДвоичныеДанные", ДвоичныеДанныеФотографии);
	ДанныеФайлаИкона.Вставить("Расширение"    , Расширение);
	
	Возврат ДанныеФайлаИкона;
	//#ГК-УИВ 29.08.2025 } ITACMT-514
КонецФункции

Процедура ЗафиксироватьДанныеПереданныхФайлов(Знач СсылкаНаДокумент, Знач ДанныеИкона)
	//#ГК-УИВ 23.08.2025 { ITACMT-514
	СписокФайлов = ДанныеИкона["PhotoUrl"];
	
	Если СписокФайлов = Неопределено Тогда
		СписокФайлов = ДанныеИкона["Photos"];
	КонецЕсли;
	
	Если ТипЗнч(СписокФайлов) <> Тип("Массив") Тогда
		СписокФайлов = Новый Массив;
		СписокФайлов.Добавить(ДанныеИкона["PhotoUrl"]);
	КонецЕсли;
	
	Менеджер = РегистрыСведений.гк_ФайлыЗамечанийИкона;
	Для Каждого СсылкаНаФайл Из СписокФайлов Цикл
		
		Если ТипЗнч(СсылкаНаФайл) = Тип("Соответствие") Тогда
			СсылкаНаФайл = СсылкаНаФайл["PhotoUrl"];
		КонецЕсли;
			
		Менеджер.ДобавитьСсылкуНаФайлДляДокумента(СсылкаНаФайл, СсылкаНаДокумент);
		
	КонецЦикла;
	//#ГК-УИВ 23.08.2025 } ITACMT-514
КонецПроцедуры

#КонецОбласти

#Область Прочее

Функция ДанныеИконаСтрокой(Знач ДанныеИкона)
	//#ГК-УИВ 29.08.2025 { ITACMT-514
	СписокКлючейЗначений = Новый Массив;
	СписокКлючейЗначений.Добавить("Ответ ICONA:");
	
	Для Каждого КлючЗначение Из ДанныеИкона Цикл
		СписокКлючейЗначений.Добавить(СтрШаблон("Ключ: %1, Значение: %2", КлючЗначение.Ключ, КлючЗначение.Значение));
	КонецЦикла;
	
	Возврат СтрСоединить(СписокКлючейЗначений, символы.ПС);
	//#ГК-УИВ 29.08.2025 } ITACMT-514
КонецФункции

Процедура ДополнительСписокДопПараметровОтборомПоДоговору(ДополнительныеПараметры)
	
	Имя = "НастройкиЗагрузкиЗамечанияСтройконтроля";
	НастройкиИспользуюся = гк_ОбщегоНазначенияПовтИсп.СлужебноеПредопределенноеЗначение(Имя);
	
	Если Не НастройкиИспользуюся Тогда
		Возврат;
	КонецЕсли;
		
	ДополнительныеПараметры.Вставить("fromDate", "2025-08-01");
	ДополнительныеПараметры.Вставить("toDate"  , "2025-08-31");
	
	ТаблицаДоговоров = гк_ОбщегоНазначенияПовтИсп.СлужебноеПредопределенныеПараметры(Имя);
	
	Если ЗначениеЗаполнено(ТаблицаДоговоров) Тогда
		СписокИдДоговоров = ТаблицаДоговоров.ВыгрузитьКолонку("Параметр");
		
		СписокИд = Новый Массив;
		Первый = Истина;
		Для Каждого Ид Из СписокИдДоговоров Цикл
			Если Первый Тогда
				СписокИд.Добавить(Ид);
				Первый = Ложь;
			Иначе
				СписокИд.Добавить(СтрШаблон("ContractId=%1", Ид));
			КонецЕсли;
		КонецЦикла;
		
		ДополнительныеПараметры.Вставить("ContractId", СтрСоединить(СписокИд, "&"));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти //#ГК-УИВ 21.08.2025 } ITACMT-514
